?start: header

header: _row+ _time_dimension_lines _time_range_dimension_lines (_end_of_header | end_of_header_with_instrument_spectral_width)

_row: filename
      | system_id
      | ngates
      | gate_range
      | gate_length
      | npulses
      | nrays
      | nwaypoints
      | scantype
      | focus_range
      | start_time
      | resolution
      | range_of_measurement


_md{key, val}: key ":\t" val _ENDL
filename: _md{"Filename", STRING}
system_id: _md{"System ID", STRING}
ngates: _md{"Number of gates", INTEGER}
gate_range: _md{"Range gate length (m)", DECIMAL}
gate_length: _md{"Gate length (pts)", INTEGER}
npulses: _md{"Pulses/ray", INTEGER}
nrays: _md{"No. of rays in file", INTEGER}
nwaypoints: _md{"No. of waypoints in file", INTEGER}
scantype: _md{"Scan type", scantype_enum}
focus_range: _md{"Focus range", DECIMAL}
start_time: _md{"Start time", STRING}
resolution: _md{"Resolution (m/s)", DECIMAL}
range_of_measurement: RANGE_OF_MEASUREMENT

RANGE_OF_MEASUREMENT: ("Altitude"|"Range") " of measurement (center of gate) = (range gate + 0.5) * Gate length" _ENDL

_time_dimension_lines: "Data line 1: " time_dimension_variables _ENDL _precisions _ENDL
_time_range_dimension_lines: "Data line 2: " time_range_dimension_variables _ENDL _precisions " - repeat for no. gates" _ENDL

time_dimension_variables: time_dimension_variable (" " " "? time_dimension_variable)*
time_range_dimension_variables: time_range_dimension_variable (" " " "? time_range_dimension_variable)*

time_dimension_variable: DECIMAL_TIME_H
                        | AZIMUTH_DEG
                        | ELEVATION_DEG
                        | PITCH_DEG
                        | ROLL_DEG

time_range_dimension_variable: RANGE_GATE
                         | DOPPLER
                         | INTENSITY
                         | BETA
                         | SPECTRAL_WIDTH

DECIMAL_TIME_H: "Decimal time (hours)"
AZIMUTH_DEG: "Azimuth (degrees)"
ELEVATION_DEG: "Elevation (degrees)"
PITCH_DEG: "Pitch (degrees)"
ROLL_DEG: "Roll (degrees)"

RANGE_GATE: "Range Gate"
DOPPLER: "Doppler (m/s)"
INTENSITY: "Intensity (SNR + 1)"
BETA: "Beta (m-1 sr-1)"
SPECTRAL_WIDTH: "Spectral Width"


_precisions: _precision ("," _precision)*
_precision: _PRECISION_FMT ("," _PRECISION_MULT)?
_PRECISION_FMT: /[ief]\d+(\.\d+)?/
_PRECISION_MULT: INTEGER "x"

_end_of_header: _END_OF_HEADER_SIMPLE

_END_OF_HEADER_SIMPLE: "****" _ENDL
end_of_header_with_instrument_spectral_width: "**** Instrument spectral width = " DECIMAL _ENDL

scantype_enum: STARE
              | STARE_OVERLAPPING
              | SECTORSCAN_STEPPED
              | VAD
              | VAD_STEPPED
              | VAD_OVERLAPPING
              | USER1_STEPPED
              | USER1_CSM_OVERLAPPING
              | USER2_STEPPED
              | USER2_CSM
              | WIND_PROFILE
              | WIND_PROFILE_OVERLAPPING
              | RHI
              | S_VAD_70_STEPPED
              | S_12_STARE_STEPPED
              | OTHER_SCANTYPE


STARE: "Stare"
STARE_OVERLAPPING: "Stare - overlapping"
SECTORSCAN_STEPPED: "SECTORSCAN - stepped"
VAD: "VAD"
VAD_STEPPED: "VAD - stepped"
VAD_OVERLAPPING: "VAD - overlapping"
USER1_STEPPED: "User file 1 - stepped"
USER1_CSM_OVERLAPPING: "User file 1 - csm - overlapping"
USER2_STEPPED: "User file 2 - stepped"
USER2_CSM: "User file 2 - csm"
WIND_PROFILE: "Wind profile"
WIND_PROFILE_OVERLAPPING: "Wind profile - overlapping"
RHI: "RHI"
S_VAD_70_STEPPED: "S_vad_70 - stepped"
S_12_STARE_STEPPED: "S_12stare - stepped"
OTHER_SCANTYPE: STRING

INTEGER: /[0-9]+/
DECIMAL: /[-+]?/ INTEGER ("." INTEGER)?
STRING: /(\w|[- .,:()=*\\])+/

_ENDL: "\r\n"
